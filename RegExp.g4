//-------------------------------------------------------------------------------------------------------------------
//                                      Lexer Rules
lexer grammar RegExp;
//-------------------------------------------------------------------------------------------------------------------


//-------------------------------------------------------------------------------------------------------------------
//Reserved words
//-------------------------------------------------------------------------------------------------------------------
ABSTRACT : 'abstract';
ASSERT : 'assert';
BOOLEAN : 'boolean';
BREAK : 'break';
BYTE : 'byte';
CASE : 'case';
CATCH : 'catch';
CHAR : 'char';
CLASS : 'class';
CONST : 'const';
CONTINUE : 'continue';
DEFAULT : 'default';
DO : 'do';
DOUBLE : 'double';
ELSE : 'else';
ENUM : 'enum';
EXTENDS : 'extends';
FINAL : 'final';
FINALLY : 'finally';
FLOAT : 'float';
FOR : 'for';
IF : 'if';
GOTO : 'goto';
IMPLEMENTS : 'implements';
IMPORT : 'import';
INSTANCEOF : 'instanceof';
INT : 'int';
INTERFACE : 'interface';
LONG : 'long';
NATIVE : 'native';
NEW : 'new';
PACKAGE : 'package';
PRIVATE : 'private';
PROTECTED : 'protected';
PUBLIC : 'public';
RETURN : 'return';
SHORT : 'short';
STATIC : 'static';
STRICTFP : 'strictfp';
SUPER : 'super';
SWITCH : 'switch';
SYNCHRONIZED : 'synchronized';
THIS : 'this';
THROW : 'throw';
THROWS : 'throws';
TRANSIENT : 'transient';
TRY : 'try';
VOID : 'void';
VOLATILE : 'volatile';
WHILE : 'while';

//-------------------------------------------------------------------------------------------------------------------*/
//Literals
//-------------------------------------------------------------------------------------------------------------------*/
//  Integer Literals
IntegerLiteral
	:	DecimalIntegerLiteral
	|	HexIntegerLiteral
	|	OctalIntegerLiteral
	|	BinaryIntegerLiteral
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
DecimalIntegerLiteral
	:	DecimalNumeral IntegerTypeSuffix?
    ;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
HexIntegerLiteral
	:	HexNumeral IntegerTypeSuffix?
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
OctalIntegerLiteral
	:	OctalNumeral IntegerTypeSuffix?
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
BinaryIntegerLiteral
	:	BinaryNumeral IntegerTypeSuffix?
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
IntegerTypeSuffix
	:	[lL]
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
DecimalNumeral
	:	'0'
	|	NonZeroDigit (Digits? | Underscores Digits)
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
Digits
	:	Digit (DigitsAndUnderscores? Digit)?
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
Digit
	:	'0'
	|	NonZeroDigit
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
NonZeroDigit
	:	[1-9]
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
DigitsAndUnderscores
	:	DigitOrUnderscore+
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
DigitOrUnderscore
	:	Digit
	|	'_'
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
Underscores
	:	'_'+
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
HexNumeral
	:	'0' [xX] HexDigits
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
HexDigits
	:	HexDigit (HexDigitsAndUnderscores? HexDigit)?
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
HexDigit
	:	[0-9a-fA-F]
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
HexDigitsAndUnderscores
	:	HexDigitOrUnderscore+
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
HexDigitOrUnderscore
	:	HexDigit
	|	'_'
	;

/*-------------------------------------------------------------------------------------------------------------------*/
fragment
OctalNumeral
	:	'0' Underscores? OctalDigits
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
OctalDigits
	:	OctalDigit (OctalDigitsAndUnderscores? OctalDigit)?
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
OctalDigit
	:	[0-7]
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
OctalDigitsAndUnderscores
	:	OctalDigitOrUnderscore+
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
OctalDigitOrUnderscore
	:	OctalDigit
	|	'_'
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
BinaryNumeral
	:	'0' [bB] BinaryDigits
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
BinaryDigits
	:	BinaryDigit (BinaryDigitsAndUnderscores? BinaryDigit)?
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
BinaryDigit
	:	[01]
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
BinaryDigitsAndUnderscores
	:	BinaryDigitOrUnderscore+
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
BinaryDigitOrUnderscore
	:	BinaryDigit
	|	'_'
	;

//-------------------------------------------------------------------------------------------------------------------
// Floating-Point Literals
FloatingPointLiteral
	:	DecimalFloatingPointLiteral
	|	HexadecimalFloatingPointLiteral
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
DecimalFloatingPointLiteral
	:	Digits '.' Digits? ExponentPart? FloatTypeSuffix?
	|	'.' Digits ExponentPart? FloatTypeSuffix?
	|	Digits ExponentPart FloatTypeSuffix?
	|	Digits FloatTypeSuffix
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
ExponentPart
	:	ExponentIndicator SignedInteger
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
ExponentIndicator
	:	[eE]
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
SignedInteger
	:	Sign? Digits
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
Sign
	:	[+-]
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
FloatTypeSuffix
	:	[fFdD]
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
HexadecimalFloatingPointLiteral
	:	HexSignificand BinaryExponent FloatTypeSuffix?
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
HexSignificand
	:	HexNumeral '.'?
	|	'0' [xX] HexDigits? '.' HexDigits
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
BinaryExponent
	:	BinaryExponentIndicator SignedInteger
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
BinaryExponentIndicator
	:	[pP]
	;

//-------------------------------------------------------------------------------------------------------------------
//  Boolean Literals
BooleanLiteral
	:	'true'
	|	'false'
	;

//-------------------------------------------------------------------------------------------------------------------
// Character Literals
CharacterLiteral
	:	'\'' SingleCharacter '\''
	|	'\'' EscapeSequence '\''
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
SingleCharacter
	:	~['\\\r\n]
	;

//-------------------------------------------------------------------------------------------------------------------
// String Literals
StringLiteral
	:	'"' StringCharacters? '"'
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
StringCharacters
	:	StringCharacter+
	;

//-------------------------------------------------------------------------------------------------------------------
fragment
StringCharacter
	:	~["\\\r\n]
	|	EscapeSequence
	;

//-------------------------------------------------------------------------------------------------------------------
// Escape Sequences for Character and String Literals
fragment
EscapeSequence
	:	'\\' [btnfr"'\\]
	|	OctalEscape
    |   UnicodeEscape // This is not in the spec but prevents having to preprocess the input
	;

//-------------------------------------------------------------------------------------------------------------------/
fragment
OctalEscape
	:	'\\' OctalDigit
	|	'\\' OctalDigit OctalDigit
	|	'\\' ZeroToThree OctalDigit OctalDigit
	;

//-------------------------------------------------------------------------------------------------------------------/
fragment
ZeroToThree
	:	[0-3]
	;

//-------------------------------------------------------------------------------------------------------------------
// This is not in the spec but prevents having to preprocess the input
fragment
UnicodeEscape
    :   '\\' 'u'+  HexDigit HexDigit HexDigit HexDigit
    ;

//-------------------------------------------------------------------------------------------------------------------
//  The Null Literal
NullLiteral
	:	'null'
	;

//-------------------------------------------------------------------------------------------------------------------
// Separators
//-------------------------------------------------------------------------------------------------------------------
LPAREN : '(';
RPAREN : ')';
LBRACE : '{';
RBRACE : '}';
LBRACK : '[';
RBRACK : ']';
SEMI : ';';
COMMA : ',';
DOT : '.';

//-------------------------------------------------------------------------------------------------------------------
//  Operators
//-------------------------------------------------------------------------------------------------------------------
ASSIGN : '=';
GT : '>';
LT : '<';
BANG : '!';
TILDE : '~';
QUESTION : '?';
COLON : ':';
EQUAL : '==';
LE : '<=';
GE : '>=';
NOTEQUAL : '!=';
AND : '&&';
OR : '||';
INC : '++';
DEC : '--';
ADD : '+';
SUB : '-';
MUL : '*';
DIV : '/';
BITAND : '&';
BITOR : '|';
CARET : '^';
MOD : '%';
ARROW : '->';
COLONCOLON : '::';

ADD_ASSIGN : '+=';
SUB_ASSIGN : '-=';
MUL_ASSIGN : '*=';
DIV_ASSIGN : '/=';
AND_ASSIGN : '&=';
OR_ASSIGN : '|=';
XOR_ASSIGN : '^=';
MOD_ASSIGN : '%=';
LSHIFT_ASSIGN : '<<=';
RSHIFT_ASSIGN : '>>=';
URSHIFT_ASSIGN : '>>>=';

//-------------------------------------------------------------------------------------------------------------------
//  Identifiers (must appear after all keywords in the grammar)
//-------------------------------------------------------------------------------------------------------------------
Identifier
	:	[a-z]([a-zA-Z]|[0-9]|'_')*//IdentifierStart IdentifierPart*
	;

Class_Identifier
	:	[A-Za-z]([a-zA-Z]|[0-9]|'_')*
	;

//-------------------------------------------------------------------------------------------------------------------
// Additional symbols not defined in the lexical specification
//-------------------------------------------------------------------------------------------------------------------
AT : '@';
ELLIPSIS : '...';

//-------------------------------------------------------------------------------------------------------------------
// Skiped Reg Exp
//-------------------------------------------------------------------------------------------------------------------
WS : [ \r\t\n]+ -> skip ;
COMMENT : '//' ~[\r\n]* '\r'? '\n' -> skip ;
BLOCK_COMMENT : '/*' .*? '*/' -> skip;